# Maintainer: Daan van Rossum <d.r.vanrossum at gmx dot de>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=llpp
pkgver=22
_pkgname=${pkgname}-1beb003
pkgrel=3
pkgdesc='Lightweight, fast and featureful PDF viewer based on MuPDF'
groups=('modified')
arch=('i686' 'x86_64')
url='http://repo.or.cz/w/llpp.git'
license=('custom')
depends=('openjpeg2' 'jbig2dec' 'freetype2' 'harfbuzz' 'libjpeg'
	'openssl' 'zlib' 'desktop-file-utils' 'mesa' 'libgl' 'fontconfig')
makedepends=('ocaml' 'libmupdf')
source=(http://repo.or.cz/llpp.git/snapshot/1beb003ca0f4ed90fda2823cb07c2eb674fc3ca4.tar.gz
        fontconfig.patch)
sha256sums=('92723eabd3521cb16d60b8dd6191ec8737240f35bf1fa9a0f82f32f8fd8f32a4'
            '531c7d0587f6bf38f99e0e3fe0d40196da229647b24e223b568b64d433c85eba')

optdepends=(
  # llpp
  'xsel: Text selection'
  # llppac
  'file: automatic file type recognition'
  "gzip: handling of MIME types 'application/x-{gzip,compress}'"
  "xz: handling of MIME type 'application/x-xz'"
  "bzip2: handling of MIME type 'application/x-bzip2'"
  'djvulibre: djvu conversion'
  'ghostscript: ps, dvi, and djvu conversion'
  'princexml: html conversion'
  'unoconv: conversion of office documents'
  'librsvg: preferred option for svg conversion'
  'inkscape: alternative option for svg conversion'
  'imagemagick: image conversion'
  'antiword: conversion of Microsoft Word (.doc) documents (option 2)'
  'zip: png and jpeg conversion'
  'texlive-core: dvi conversion'
)
options=('!strip')

prepare() {
  cd $_pkgname
  patch -Np1 -i ../fontconfig.patch
}

build() {
  cd $_pkgname
  sed -i -e 's+-I \$srcdir/mupdf/include -I \$srcdir/mupdf/thirdparty/freetype/include+-I /usr/include/freetype2+' build.sh
  sed -i -e 's+-lmupdf +-lfreetype -lz -lharfbuzz -ljbig2dec -lopenjp2 -ljpeg -lfontconfig -lmupdf +' build.sh
  sed -i -e 's+-L\$srcdir/mupdf/build/native ++' build.sh
  sed -i -e 's+-D_GNU_SOURCE +-D_GNU_SOURCE -DUSE_FONTCONFIG +' build.sh
  sh build.sh build/
}

package() {
  cd $_pkgname
  install -Dm755 build/llpp "$pkgdir"/usr/bin/llpp
  install -Dm755 misc/llppac "$pkgdir"/usr/bin/llppac

  install -Dm0644 misc/llpp.desktop "$pkgdir"/usr/share/applications/llpp.desktop
  install -Dm0644 README "$pkgdir"/usr/share/licenses/llpp/README
}
