# Contributor: noonov <noonov@gmail.com>

_pkgname=uim
pkgname=${_pkgname}-git
pkgver=1.8.0.alpha.r308.g2a47b41
pkgrel=1
epoch=1
pkgdesc="A multilingual input method framework"
groups=('modified')
arch=('i686' 'x86_64')
url="https://github.com/uim/uim"
license=('BSD')
depends=('gtk2')
makedepends=('git' 'intltool' 'perl' 'ruby' 'librsvg' 'asciidoc' 'ed')
provides=('uim')
conflicts=('uim' 'uim-svn')
install=uim-git.install
source=("git+https://github.com/uim/uim.git"
        "gtk3-caret-state-fix.patch")
md5sums=('SKIP'
         'de4c803f68f49a5a965c09c83df1451f')
sha256sums=('SKIP'
            '1f801d57f73731168553579d2ac8cd406b9ef635bd967abbef7b31f58fccdc9e')

pkgver() {
  cd ${srcdir}/${_pkgname}
  git describe --long --tags | sed -r 's/^uim-//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  touch -c ${srcdir}/${_pkgname}/scm/json-parser-expanded.scm
  cd "${srcdir}/${_pkgname}"
  patch -Np1 -i ../gtk3-caret-state-fix.patch
}

build() {
  cd ${srcdir}/${_pkgname}

  ./make-wc.sh --prefix=/usr --libexecdir=/usr/lib/uim \
      --with-qt4 --with-qt4-immodule \
      --with-qt5 --with-qt5-immodule
  make
}

package() {
  cd ${srcdir}/${_pkgname}

  make -j1 DESTDIR=${pkgdir} install

  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
