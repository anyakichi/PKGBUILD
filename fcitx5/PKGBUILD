# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5
pkgver=5.1.5
pkgrel=1
pkgdesc="Next generation of fcitx"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5"
license=('GPL')
conflicts=('fcitx')
groups=('fcitx5-im' 'modified')
depends=('cairo' 'enchant' 'iso-codes' 'libgl' 'libxkbcommon-x11' 'pango' 'systemd' 'wayland'
         'xcb-imdkit' 'xcb-util-wm' 'libxkbfile' 'gdk-pixbuf2')
makedepends=('extra-cmake-modules' 'ninja' 'wayland-protocols' 'fmt')
source=("https://download.fcitx-im.org/fcitx5/fcitx5/fcitx5-${pkgver}_dict.tar.xz"{,.sig}
        vi-cooperative-mode.patch)
sha512sums=('d4b5400401b9941cf23dcc5682b026a576bfe57f93a8f65e93127aadedde853c3d026105fac893aaf872a7c8418f3a0d9487e42bcc1427e62c7439f7fb0a3a80'
            'SKIP'
            'a9d63dcfad9349755393a94ea163137c383338db5281d5f002abf99ebb7a71534e4c524b3a9ac6673eeb2f2aa65a55e7301a04f49b75745f88fb21410839a90c')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../vi-cooperative-mode.patch
}

build() {
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname-$pkgver
  ninja test
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
