# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=firefox-tridactyl
pkgver=1.24.4
pkgrel=1
pkgdesc="Replace Firefox's control mechanism with one modelled on Vim"
arch=(any)
url=https://github.com/tridactyl/tridactyl
license=(Apache-2.0)
groups=(firefox-addons modified)
makedepends=(
  git
  node-gyp
  python
  strip-nondeterminism
  yarn
)
source=("git+$url.git#commit=$pkgver?signed" editor-contenteditable.patch)
b2sums=('1cf24ac2aff7e9caf260adc2b491dfb43a39b7a67e3734062a54fa19af7b66d18a361af5794169ad7232bc56f665d3872dcf4a5e0fd0ec1ef270e38779d681c1'
        '4b6bee7d939ef40c4471f9e19a94234b5312759706cad392bfa6e08f034b51ac2feddca86f347657c7a08b9c156b27f8d3e84ae27e1ecd8bfa0f928150fa9b0e')
validpgpkeys=(15D03C86837936A0A638AA2D2BB8C36BB504BFF3) # Oliver Blanthorn <freedom4cows@gmail.com>

prepare() {
  cd tridactyl
  patch -Np1 -i ../editor-contenteditable.patch
  yarn install --frozen-lockfile --non-interactive
}

build() {
  cd tridactyl
  scripts/sign nosignstable
  strip-nondeterminism -t zip web-ext-artifacts/tridactyl-$pkgver.xpi
}

package() {
  cd tridactyl
  install -Dm644 web-ext-artifacts/tridactyl-$pkgver.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/tridactyl.vim@cmcaine.co.uk.xpi
}
