# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Ekl√∂f <daniel at ekloef dot se>

pkgbase=foot
pkgdesc='Fast, lightweight, and minimalistic Wayland terminal emulator'
pkgname=($pkgbase $pkgbase-terminfo)
pkgver=1.15.0
pkgrel=1
url="https://codeberg.org/dnkl/$pkgbase"
arch=(x86_64)
license=(MIT)
groups=('modified')
makedepends=(fcft
             fontconfig
             libutf8proc
             libxkbcommon
             llvm
             meson
             ncurses
             ninja
             pixman
             python
             scdoc
             sway
             tllist
             wayland
             wayland-protocols)
optdepends=('libutempter: utmp logging')
backup=(etc/xdg/foot/foot.ini)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz" add-extended-underlines.patch)
sha256sums=('5b6fcec57cedca350c36fa46534bd7724ed414cc292516b74567d070b7062f07'
            '1d41447c2690f465770df490ead727729689b81979b493c51c2d5e31a3fd205a')

prepare() {
  cd "$pkgbase"
  patch -Np1 -i ../add-extended-underlines.patch
}

build() {
	cd "$pkgbase"
	./pgo/pgo.sh \
		full-headless-sway \
		. build \
		-Dterminfo=disabled \
		-Dext-underline=true \
		--prefix=/usr \
		--wrap-mode=nodownload
	sed 's/@default_terminfo@/foot-extra/g' foot.info |
		tic -x -o build -e foot-extra,foot-extra-direct -
}

check() {
	cd "$pkgbase"
	ninja -C build test
}

package_foot() {
	optdepends=('foot-terminfo: exta non-standard features over terminfo included in ncurses'
	            'libnotify: desktop notifications'
	            'xdg-utils: URI launching')
	depends=(fcft
	         fontconfig
	         libfcft.so
	         libutf8proc
	         libxkbcommon
	         ncurses
	         pixman
	         wayland)
	cd "$pkgbase"
	DESTDIR="$pkgdir" ninja -C build install
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_foot-terminfo() {
	pkgdesc="Extra non-standard terminfo files for $pkgbase, a Wayland terminal emulator"
	depends=(ncurses)
	cd "$pkgbase"
	install -Dm0644 -t "$pkgdir/usr/share/terminfo/f/" build/f/foot-extra*
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
