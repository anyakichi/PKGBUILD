# $Id: PKGBUILD 190709 2016-09-28 13:13:24Z bluewind $
# Maintainer:
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Thorsten Tasch <tht@thorstentasch.de>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=rsnapshot
pkgver=1.4.2
pkgrel=2
pkgdesc="A remote filesystem snapshot utility"
groups=('modified')
arch=('any')
url="http://www.rsnapshot.org"
license=('GPL')
depends=('perl' 'rsync' 'openssh' 'perl-lchown')
backup=('etc/rsnapshot.conf')
source=("https://github.com/rsnapshot/rsnapshot/releases/download/$pkgver/${pkgname}-${pkgver}.tar.gz"
        'rsnapshot'
		'0001-added-missing-back.patch'
		"pod-fix.patch"
                'lvm-undef-fix.patch'
                'mount_args.patch')
md5sums=('860382f19e38fc649f9447397b54e442'
         '4cd16eaa51d58a652cffde945985e91c'
         'a7075d9366eda5e699be86f7ffe8246e'
         '0d889cbef6fddf8401611bc16ed63c5c'
         '7b3bc1fa5b5653464fa163946eedb147'
         '1e06cae5c52c04e6fefe68c05e428316')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i "$srcdir/0001-added-missing-back.patch"
	patch -p1 -i "$srcdir/pod-fix.patch"
	patch -p1 -i "$srcdir/lvm-undef-fix.patch"
	patch -p1 -i "$srcdir/mount_args.patch"
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
	sed -i 's:/usr/bin/pod2man:/usr/bin/core_perl/pod2man:' Makefile
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR=${pkgdir} install
	install -Dm755 utils/rsnapreport.pl "$pkgdir/usr/bin/rsnapreport.pl"
	mv ${pkgdir}/etc/rsnapshot.conf.default ${pkgdir}/etc/rsnapshot.conf
	install -Dm644 ${srcdir}/rsnapshot ${pkgdir}/etc/logrotate.d/rsnapshot
}
